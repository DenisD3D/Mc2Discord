apply from: '../properties.gradle'

buildscript {
    repositories {
        gradlePluginPortal()
        maven { url = 'https://maven.minecraftforge.net' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'gradle.plugin.com.github.johnrengelman', name: 'shadow', version: '7.1.0'
    }
}
        
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.

version = projectVersion
group = projectGroup
archivesBaseName = modid + '-forge-1.12.2'

java.toolchain.languageVersion = JavaLanguageVersion.of(8) // Mojang ships Java 8 to end users.

minecraft {
    mappings channel: 'snapshot', version: '20171003-1.12'

    runs {
        server {
            workingDirectory file('run_server')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'info'
            arg 'nogui'
            singleInstance true
        }
    }
}

repositories {
    maven { url = 'https://jitpack.io' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2855'

    shadow(project(path: ':mc2discord-core', configuration: 'shadow'))
    shadow(group: 'com.electronwill.night-config', name: 'toml', version: '3.6.0')

    implementation(group: 'io.netty', name: 'netty-all') {
        version {
            strictly '4.1.65.Final'
        }
    } // Fix compatibility between Minecraft & Reactor netty version in dev

    configurations.implementation.extendsFrom(configurations.shadow)
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    classifier 'slim'
    manifest {
        attributes([
                "Specification-Title"   : "Mc2discord",
                "Specification-Vendor"  : "DenisD3D",
                "Specification-Version" : "1",
                "Implementation-Title"  : project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" : "DenisD3D",
        ])
    }
}

shadowJar {
    classifier ''

    configurations = [project.configurations.shadow]

    relocate('discord4j', repackGroup + 'discord4j')
    relocate('com.austinv11.servicer', repackGroup + 'com.austinv11.servicer')
    relocate('com.discord4j', repackGroup + 'com.discord4j')
    relocate('com.fasterxml.jackson', repackGroup + 'com.fasterxml.jackson')
    relocate('com.iwebpp', repackGroup + 'com.iwebpp')
    relocate('com.vdurmont.emoji', repackGroup + 'com.vdurmont.emoji')
    relocate('com.github.benmanes.caffeine', repackGroup + 'com.github.benmanes.caffeine')
    relocate('com.google.errorprone', repackGroup + 'com.google.errorprone')
    relocate('io.netty', repackGroup + 'io.netty')
    relocate('org.apache.commons.logging', repackGroup + 'org.apache.commons.logging')
    relocate('org.apache.commons.collections4', repackGroup + 'org.apache.commons.collections4')
    relocate('org.apache.commons.codec', repackGroup + 'org.apache.commons.codec')
    relocate('org.apache.logging.slf4j', repackGroup + 'org.apache.logging.slf4j')
    relocate('org.checkerframework', repackGroup + 'org.checkerframework')
    relocate('org.json', repackGroup + 'org.json')
    relocate('org.reactivestreams', repackGroup + 'org.reactivestreams')
    relocate('org.slf4j', repackGroup + 'org.slf4j')
    relocate('org.immutables', repackGroup + 'org.immutables')
    relocate('reactor', repackGroup + 'reactor')

    relocate('com.electronwill.nightconfig', repackGroup + 'com.electronwill.nightconfig')

    exclude('module-info.class')
}

reobf {
    shadowJar {}
}

jar.finalizedBy('reobfJar')